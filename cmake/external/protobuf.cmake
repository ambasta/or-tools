FIND_PACKAGE(ZLIB REQUIRED)

# SET(Protobuf_INCLUDE_DIRS ${CMAKE_CURRENT_BINARY_DIR}/protobuf_project/src/protobuf/src)
SET(Protobuf_URL https://github.com/google/protobuf)

IF(MSVC)
    SET(Protobuf_ADDITIONAL_CMAKE_OPTIONS "${Protobuf_ADDITIONAL_CMAKE_OPTIONS} -G \"NMake MakeFiles\"")
ENDIF()

ExternalProject_Add(Protobuf_project
        PREFIX Protobuf
        GIT_REPOSITORY ${Protobuf_URL}
        GIT_TAG "v${Protobuf_VERSION}"
        DOWNLOAD_DIR "${DOWNLOAD_LOCATION}"
        UPDATE_COMMAND ""
        BUILD_IN_SOURCE 1
        SOURCE_DIR ${CMAKE_CURRENT_BINARY_DIR}/protobuf_project/src/protobuf
        INSTALL_DIR ${CMAKE_CURRENT_BINARY_DIR}/install
        CONFIGURE_COMMAND ${CMAKE_COMMAND} cmake/
        -Dprotobuf_BUILD_TESTS=OFF
        -DBUILD_STATIC_LIBS=ON
        -DCMAKE_POSITION_INDEPENDENT_CODE=ON
        -DCMAKE_INSTALL_PREFIX=${CMAKE_CURRENT_BINARY_DIR}/install
        ${Protobuf_ADDITIONAL_CMAKE_OPTIONS})

ADD_LIBRARY(Protobuf STATIC IMPORTED)

IF(EXISTS ${CMAKE_CURRENT_BINARY_DIR}/install/lib/libprotobuf.a)
    SET_PROPERTY(TARGET Protobuf
        PROPERTY IMPORTED_LOCATION
        ${CMAKE_CURRENT_BINARY_DIR}/install/lib/libprotobuf.a)
ELSE()
    SET_PROPERTY(TARGET Protobuf
        PROPERTY IMPORTED_LOCATION
        ${CMAKE_CURRENT_BINARY_DIR}/install/lib64/libprotobuf.a)
ENDIF()
SET_PROPERTY(TARGET Protobuf
    PROPERTY INCLUDE_DIRECTORIES
    $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/install/include>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}>)

ADD_DEPENDENCIES(Protobuf Protobuf_project)
SET(Protobuf_LIBRARIES "")
LIST(APPEND Protobuf_LIBRARIES Protobuf ${ZLIB_LIBRARIES})

UNSET(Protobuf_PROTOC_EXECUTABLE CACHE)
ADD_EXECUTABLE(Protobuf_PROTOC_EXECUTABLE IMPORTED)
SET_PROPERTY(TARGET Protobuf_PROTOC_EXECUTABLE
    PROPERTY IMPORTED_LOCATION
    ${CMAKE_CURRENT_BINARY_DIR}/install/bin/protoc)
ADD_DEPENDENCIES(Protobuf_PROTOC_EXECUTABLE Protobuf_project)

SET(Protobuf_PROTOC_EXECUTABLE Protobuf_PROTOC_EXECUTABLE)

INSTALL(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/install/include/google
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}
    COMPONENT Devel)
