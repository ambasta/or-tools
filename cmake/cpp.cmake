FIND_PACKAGE(gflags ${gflags_VERSION} QUIET)
IF(BUILD_DEPS)
    SET(gflags_FOUND False)
ENDIF()
IF(NOT gflags_FOUND)
    MESSAGE(STATUS "Did not find system gflags or forced build. Building as an external project")
    INCLUDE(cmake/external/gflags.cmake)
ENDIF()

FIND_PACKAGE(glog ${glog_VERSION} QUIET)
IF(BUILD_DEPS)
    SET(glog_FOUND False)
ENDIF()
IF(NOT glog_FOUND)
    MESSAGE(STATUS "Did not find system glog. Building as an external project.")
    INCLUDE(cmake/external/glog.cmake)
ENDIF()

FIND_PACKAGE(Cbc ${Cbc_VERSION} QUIET)
IF(BUILD_DEPS)
    SET(Cbc_FOUND False)
ENDIF()
IF(NOT Cbc_FOUND)
    IF(NOT MSVC)
        MESSAGE(STATUS "Did not find system coin-cbc. Building as an external project.")
        INCLUDE(cmake/external/cbc.cmake)
        ADD_DEFINITIONS(-DUSE_CLP -DUSE_CBC)
    ENDIF()
ELSE()
    ADD_DEFINITIONS(-DUSE_CLP -DUSE_CBC)
ENDIF()

FILE(GLOB_RECURSE proto_files RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} "ortools/*.proto")
PROTOBUF_GENERATE_CPP(PROTO_SRCS PROTO_HDRS ${proto_files})
ADD_CUSTOM_TARGET(ProtoSources ALL DEPENDS ${PROTO_SRCS})
ADD_LIBRARY(${PROJECT_NAME}Proto OBJECT ${PROTO_SRCS} ${PROTO_HDRS})
SET_TARGET_PROPERTIES(${PROJECT_NAME}Proto PROPERTIES POSITION_INDEPENDENT_CODE ON)
TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME}Proto
    PRIVATE
    ${Protobuf_INCLUDE_DIRS}
    ${gflags_INCLUDE_DIRS}
    ${glog_INCLUDE_DIRS}
    ${Cbc_INCLUDE_DIRS})

IF(NOT Cbc_FOUND)
    IF(NOT MSVC)
        ADD_DEPENDENCIES(ProtoSources Cbc)
    ENDIF()
ENDIF()

IF(NOT glog_FOUND)
    ADD_DEPENDENCIES(ProtoSources glog)
ENDIF()

SET(SUBTARGETS "")

FOREACH(SUBPROJECT base util lp_data glop graph algorithms sat bop linear_solver constraint_solver)
    ADD_SUBDIRECTORY(ortools/${SUBPROJECT})
    LIST(APPEND SUBTARGETS "$<TARGET_OBJECTS:${PROJECT_NAME}_${SUBPROJECT}>")
ENDFOREACH()
LIST(APPEND SUBTARGETS "$<TARGET_OBJECTS:${PROJECT_NAME}Proto>")

ADD_LIBRARY(${PROJECT_NAME} SHARED ${SUBTARGETS})

IF(Cbc_FOUND)
    TARGET_LINK_LIBRARIES(${PROJECT_NAME} PUBLIC ${Cbc_LIBRARIES})
ELSE()
    TARGET_LINK_LIBRARIES(${PROJECT_NAME} PRIVATE ${Cbc_LIBRARIES})
ENDIF()

IF(gflags_FOUND)
    TARGET_LINK_LIBRARIES(${PROJECT_NAME} PUBLIC ${gflags_LIBRARIES})
ELSE()
    TARGET_LINK_LIBRARIES(${PROJECT_NAME} PRIVATE ${gflags_LIBRARIES})
ENDIF()

IF(Protobuf_FOUND)
    TARGET_LINK_LIBRARIES(${PROJECT_NAME} PUBLIC ${Protobuf_LIBRARIES})
ELSE()
    TARGET_LINK_LIBRARIES(${PROJECT_NAME} PRIVATE ${Protobuf_LIBRARIES})
ENDIF()

IF(glog_FOUND)
    TARGET_LINK_LIBRARIES(${PROJECT_NAME} PUBLIC ${glog_LIBRARIES})
ELSE()
    TARGET_LINK_LIBRARIES(${PROJECT_NAME} PRIVATE ${glog_LIBRARIES})
ENDIF()

TARGET_LINK_LIBRARIES(${PROJECT_NAME} PUBLIC ${CMAKE_THREAD_LIBS_INIT})

TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME}
    PRIVATE
    ${Protobuf_INCLUDE_DIRS}
    ${gflags_INCLUDE_DIRS}
    ${glog_INCLUDE_DIRS}
    ${Cbc_INCLUDE_DIRS})

ADD_DEPENDENCIES(${PROJECT_NAME} ProtoSources)
ADD_SUBDIRECTORY(examples/cpp)

GENERATE_EXPORT_HEADER(${PROJECT_NAME})
SET_PROPERTY(TARGET ${PROJECT_NAME} PROPERTY VERSION ${PROJECT_VERSION})
SET_PROPERTY(TARGET ${PROJECT_NAME} PROPERTY SOVERSION ${PROJECT_VERSION_MAJOR})
SET_PROPERTY(TARGET ${PROJECT_NAME} PROPERTY INTERFACE_${PROJECT_NAME}_MAJOR_VERSION ${PROJECT_VERSION_MAJOR})
SET_PROPERTY(TARGET ${PROJECT_NAME}
    PROPERTY INCLUDE_DIRECTORIES 
    $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/install/include/coin>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}/coin>)
GET_PROPERTY(COMPILE_DEFINITIONS DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY COMPILE_DEFINITIONS)
TARGET_COMPILE_DEFINITIONS(${PROJECT_NAME} PRIVATE ${COMPILE_DEFINITIONS} INTERFACE ${COMPILE_DEFINITIONS})

INSTALL(TARGETS ${PROJECT_NAME}
    EXPORT ${PROJECT_NAME}Targets
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
    INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})
INSTALL(DIRECTORY ortools
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    COMPONENT Devel
    FILES_MATCHING PATTERN "*.h")
INSTALL(FILES
    ${PROJECT_BINARY_DIR}/${PROJECT_NAME}_export.h
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}/
    COMPONENT Devel)

INSTALL(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/ortools
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    COMPONENT Devel
    FILES_MATCHING PATTERN "*.pb.h"
    PATTERN CMakeFiles EXCLUDE)

WRITE_BASIC_PACKAGE_VERSION_FILE("${CMAKE_CURRENT_BINARY_DIR}/ortools/${PROJECT_NAME}ConfigVersion.cmake"
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY AnyNewerVersion)
EXPORT(EXPORT ${PROJECT_NAME}Targets
    FILE "${CMAKE_CURRENT_BINARY_DIR}/ortools/${PROJECT_NAME}Targets.cmake"
    NAMESPACE ${PROJECT_NAME}::)
CONFIGURE_FILE(cmake/${PROJECT_NAME}Config.cmake.in
    "${CMAKE_CURRENT_BINARY_DIR}/ortools/${PROJECT_NAME}Config.cmake"
    @ONLY)

SET(ConfigPackageLocation ${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME})
INSTALL(EXPORT ${PROJECT_NAME}Targets
    FILE ${PROJECT_NAME}Targets.cmake
    NAMESPACE ${PROJECT_NAME}::
    DESTINATION ${ConfigPackageLocation})
INSTALL(FILES
    "${CMAKE_CURRENT_BINARY_DIR}/ortools/${PROJECT_NAME}Config.cmake"
    "${CMAKE_CURRENT_BINARY_DIR}/ortools/${PROJECT_NAME}ConfigVersion.cmake"
    DESTINATION ${ConfigPackageLocation}
    COMPONENT Devel)
